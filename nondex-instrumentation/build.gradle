
description = 'nondex-instrumentation'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile group: 'org.ow2.asm', name: 'asm-all', version:'5.1'
    compile project(':nondex-common')
}

apply plugin: 'checkstyle'

checkstyle {
    config = resources.text.fromFile("checkstyle.xml", "UTF-8")
    showViolations = true
    ignoreFailures = false
    toolVersion = "6.17"
}

shadowJar {
    relocate 'org.objectweb.asm', 'edu.illinois.nondex.instr.org.objectweb.asm'
    baseName = 'nondex-instrumentation-all'
    classifier = null
    version = null
}

ext {
    nonDexInstrJar = shadowJar.archivePath
    nonDexOutputJar = file(project.buildDir.absolutePath + '/out.jar')
}

tasks.withType(Checkstyle) {
    exclude '**/*ASMDump.java'
}

task genOut(type: Exec, dependsOn: shadowJar) {
    executable 'java'
    args '-jar', "$shadowJar.archivePath", "$nonDexOutputJar"
}

jar {
    manifest {
        attributes('Main-Class': 'edu.illinois.nondex.instr.Main')
    }
    destinationDir
    from { configurations.compile.collect { zipTree it } }
}

configurations {
    outJar
}

artifacts {
    outJar file: file(nonDexOutputJar), builtBy: genOut
}
